#!/usr/bin/perl

# SkyKitteh at github: http://github.com/ollej/skykitteh
# Mojolicious documentation: http://mojolicio.us/
#
# Neat things to add:
# * Revision control, rollback to earlier version.
# * Syntax highlighting
# * Logo - Kitty smiley in cloud?
# * AJAX updating / JSON responses for backend
# * See more in issues on github page. See above.

use warnings;
use strict;

use Text::Diff;
use File::Path 'mkpath';
use File::Slurp qw(read_file write_file);
use Digest::MD5 qw(md5_hex);

use Mojolicious::Lite;

#plugin 'console_logger';

$ENV{MOJO_TMPDIR} = '/tmp/';

get '/' => sub {
  my $self = shift;
  my $code = read_file($0);
  my $data = {
    'filename' => $0,
    'checksum', md5_hex($code),
    'code' => $code,
    'modules' => list_modules(),
    'motd' => get_motd(),
  };
  my $format = $self->param('format') || '';
  if ($format eq 'json') {
    $self->render_json($data);
  } else {
    $self->stash($data);
    $self->render('showform');
  }
};

get '/skykitteh.js' => sub {
  my $self = shift;
  my $code = read_file('skykitteh.js');
  $self->render(text => $code, format => 'js');
};

get '/public/(.format)/(*filename)' => sub {
  my $self = shift;
  my $format = $self->stash('format');
  my $filename = "./public/$format/" . $self->stash('filename');
  my $code = read_file($filename);
  $self->render(text => $code, format => $format);
};

get '/edit/(*filename)' => sub {
  my $self = shift;
  my $filename = $self->stash('filename');
  if (-d $filename) {
    return $self->render('error', msg => "No can haz edit dir!");
  }
  my $code = read_file($filename, err_mode => 'quiet');
  my $data = {
    'checksum', md5_hex($code),
    'code' => $code,
    'modules' => list_modules(),
    'motd' => get_motd(),
  };
  my $format = $self->param('format') || '';
  if ($format eq 'json') {
    $self->render_json($data);
  } else {
    $self->stash($data);
    $self->render('showform');
  }
};

post '/' => sub {
	my $self = shift;
	my $filename = $self->param('filename');
	my $code = $self->param('code');
	my $base = $self->param('base');
	$code =~ s/\r$//gm;
	$base =~ s/\r$//gm;

	# Uploadz
	my $uploadfile = $self->req->upload('uploadfile');
	app->log->info("upload: " . $self->req->param('uploadfile'));
	if ($uploadfile) {
	    app->log->info("Upload file:" . $uploadfile->file);
	    app->log->info("Upload file:" . $uploadfile->filename);
	    if ( -e './' . $uploadfile->filename) {
		return $self->render('error', 'msg' => 'Already haz file!');
	    }
	    $uploadfile->move_to('./' . $uploadfile->filename);
	    $self->redirect_to('/');
	    return;
	}

	if (!$filename) {
	    $self->stash('msg' => "Missing filename.");
	    $self->render('error');
	    return;
	}

	if ($filename =~ m{^(.+/)[^/]+$} && ! -d $1) {
	    unless (mkpath($1, {error => \my $err}) && -d $1) {
		$self->stash('msg' => "Don't confuse the kitteh.");
		$self->render('error');
		return;
	    }
	}

	my $commit_message = $self->param('commit_message');
	write_file("/tmp/commit_message.txt", $commit_message);
	
	my $oldcode = read_file($filename, err_mode => 'quiet');
	if ($self->param('checksum') ne md5_hex($oldcode)) {
		$self->stash('msg' => "Code nomz changed, MD5 checksum misssymash.");
		my @diff = split(/\n/, diff(\$base, \$oldcode));
		$self->stash('diff' => \@diff);
		$self->render('error');
		return;
	}
	write_file($filename, { binmode => ':utf8', atomic => 1 }, $code);

        my $report = `perl skykitteh.t`;
	if ($? || $report !~ /ok \d/) {
	    write_file("/tmp/commit_message.txt", 'Near deth experiuns.');
	    write_file($filename, { binmode => ':utf8', atomic => 1 }, $oldcode);
	    $self->stash('msg' => "Please don't kill SkyKitteh.");
	    $self->render('error');
	    return;
	}

	$self->redirect_to('/');
};

get '/motd' => sub {
    my $self = shift;
    my $add = $self->param('add');
    if ($add) {
	store_motd($add);
    }

    my $motd = get_motd();
    $self->stash('msg' => $motd);
    $self->render('motd');
};

sub store_motd
{
    my ($motd) = @_;

    if (open MOTD, '>>', 'motd.txt') {
	print MOTD $motd."\n";
	close MOTD;
    }
}

sub get_motd
{
    my $motd = 'I can haz code nomz?';

    if (open MOTD, '<', 'motd.txt') {
	my @motds = <MOTD>;
	close MOTD;
	if (@motds) {
	    $motd = @motds[int(rand(scalar(@motds)))];
	}
    }

    return $motd;
}

sub list_modules
{
  use HTML::Perlinfo;
  my $m = HTML::Perlinfo::Modules->new( full_page => 0 );
  return $m->print_modules();
}

app->start;
