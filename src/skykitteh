#!/usr/bin/perl

# Skykitteh at github: http://github.com/ollej/skykitteh
# Mojolicious documentation: http://mojolicio.us/
#
# Neat things to add:
# * Perl check before new code is saved.
# * Checksum for file added to output and checked before saving.
# * Revision control, rollback to earlier version.
# * Syntax highlighting
# * Logo - Kitty smiley in cloud?
# * Move html layout into separate template
# * AJAX updating / JSON responses for backend
# test

use warnings;
use strict;
use Mojolicious::Lite;
use File::Slurp qw(read_file write_file);
use Digest::MD5 qw(md5_hex);

get '/' => sub {
	my $self = shift;
	my $code = read_file($0);
	$self->stash('code' => $code);
	$self->stash('checksum', md5_hex($code));
	$self->render('showform');
};

post '/' => sub {
	my $self = shift;
	my $code = $self->param('code');
	$code =~ s/\r$//gm;
	my $oldcode = read_file($0);
	if ($self->param('checksum') != md5_hex($oldcode)) {
		$self->stash('msg' => "Code has been updated since you loaded the page, the MD5 checksum didn't match.");
		$self->render('error');
		return;
	}
	write_file($0, { binmode => ':utf8', atomic => 1 }, $code);
        my @compile = `perl -c $0 2>&1`;
	if (!grep {/syntax OK/} @compile) {
	        write_file($0, { binmode => ':utf8', atomic => 1 }, $oldcode);
		$self->stash('msg' => "New SkyKitteh did not compile.");
		$self->render('error');
		return;
	}
	$self->redirect_to('/');
};

app->start;
__DATA__

@@ error.html.ep
<h1>Error!</h1>
<p><%= $msg %></p>

@@ showform.html.ep
<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd"> 
<html xmlns="http://www.w3.org/1999/xhtml"> 
 
<head> 
	<meta http-equiv="Content-Type" content="text/html; charset=utf-8" /> 
	<title>Skykitteh</title>
	<style type="text/css">
		#header {
			background-image: -webkit-gradient(
				linear,
				left bottom,
				left top,
				color-stop(0.26, #A3A3A3),
				color-stop(0.63, #E8E8E8)
				);
				background-image: -moz-linear-gradient(
				center bottom,
				#A3A3A3 26%,
				#E8E8E8 63%
			);
			height:50px;
			text-align: center;
		}
		h1,a {
		font-family: "Myriad pro", helvetica;
		}
	</style>
</head>
<body>

<div id="header">
	<h1>=^..^= Skykitteh =^..^=</h1>
</div>

<form action="" method="post">
	<div style="display: block; padding: 100px">
		<textarea name="code" rows="45" style="font-family: monospace; width: 100%; height: 100%"><%= $code %></textarea>
        </div>	
	<input type="hidden" name="checksum" value="<%= $checksum %>" />
	<input type="submit" name="submit" value="Evolve!" />
	<a href="http://github.com/ollej/skykitteh">http://github.com/ollej/skykitteh</a>
        

</form>
</body>
</html>
